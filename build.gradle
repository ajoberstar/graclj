buildscript {
    repositories {
        jcenter()
        maven { url = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2'
        classpath 'org.ajoberstar:semver-vcs-gradle-grgit:0.1.0'
    }
}

apply plugin: 'org.sonarqube'
apply plugin: 'org.ajoberstar.semver-vcs-grgit'

allprojects {
    group = 'org.graclj'

    plugins.withId('java') {
        repositories {
            jcenter()
        }
    }

    plugins.withId('maven-publish') {
        publishing {
            repositories {
                mavenLocal()
            }
        }
    }

    plugins.withId('com.jfrog.bintray') {
        bintray {
            if (project.hasProperty('bintrayUser')) {
                user = bintrayUser
                key = bintrayKey
            }

            publications = ['main']

            publish = false

            pkg {
                userOrg = 'graclj'
                repo = 'maven'
                name = 'graclj'

                websiteUrl = 'https://github.com/graclj/graclj'
                issueTrackerUrl = 'https://github.com/graclj/graclj/issues'
                vcsUrl = 'https://github.com/graclj/graclj.git'

                licenses = ['EPL-1.0']
                labels = ['gradle', 'clojure', 'clojurescript', 'plugin']

                publicDownloadNumbers = true

                githubRepo = 'graclj/graclj'
                githubReleaseNotesFile = 'README.md'

                version {
                    name = project.version
                    vcsTag = project.version
                    attributes = [
                        'gradle-plugin': ['org.graclj.clojure-lang:org.graclj:graclj-plugin', 'org.graclj.clojure-test-suite:org.graclj:graclj-plugin']
                    ]
                }
            }
        }
    }
}

sonarqube {
    properties {
        property 'sonar.projectVersion', version.toString().split('-')[0]
    }
}
