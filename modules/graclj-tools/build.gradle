plugins {
    id 'java'
    id 'maven-publish'
}

sourceSets.main {
    resources.srcDir 'src/main/clojure'
}
sourceSets.test {
    resources.srcDir 'src/test/clojure'
}

dependencies {
    compile 'org.clojure:clojure:1.8.0'
    compile 'org.clojure:tools.namespace:0.2.11'
    compile 'junit:junit:4.12'
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
}

ext.tools = new File(buildDir, 'tools')

task compileTools(type: JavaExec) {
    classpath jar
    classpath configurations.compile
    classpath tools
    main 'clojure.main'
    args '--main', 'org.graclj.tools.compiler.clojure', file('src/main/clojure'), tools

    doFirst {
        tools.mkdirs()
    }
}



test {
    dependsOn compileTools
    classpath += files(tools)
    classpath += files('build/resources/test')
    testClassesDir = tools
    testLogging {
        events = ['started', 'passed', 'failed']
    }
}

