plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.6'
}

sourceSets.main {
    resources.srcDir 'src/main/clojure'
}
sourceSets.test {
    resources.srcDir 'src/test/clojure'
}

dependencies {
    compile 'org.clojure:clojure:1.8.0'
    compile 'org.clojure:tools.namespace:0.2.11'

    compile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

ext.cljClasses = new File(buildDir, 'classes-clj')

task compileClj(type: JavaExec) {
    classpath file('src/main/clojure')
    classpath configurations.compile
    classpath cljClasses
    main 'clojure.main'
    args '--main', 'org.graclj.tools.compiler.clojure', 'src/main/clojure', cljClasses
    doFirst {
        cljClasses.mkdirs()
    }
}

jar {
    from cljClasses
    dependsOn compileClj
}
