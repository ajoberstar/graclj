plugins {
    id 'java'
    id 'maven-publish'
}

sourceSets.main {
    resources.srcDir 'src/main/clojure'
}
sourceSets.test {
    resources.srcDir 'src/test/clojure'
}

dependencies {
    compile 'org.clojure:clojure:1.8.0'
    compile 'org.clojure:tools.namespace:0.2.11'
    compile 'junit:junit:4.12'
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
        }
    }
}

ext.tests = new File(buildDir, 'clj-tests')

task prepareCljTests(type: Copy) {
    from sourceSets.main.allSource
    from sourceSets.test.allSource
    into tests
}

task compileCljTests(type: JavaExec) {
    dependsOn prepareCljTests
    classpath jar
    classpath configurations.compile
    classpath file('build/resources/test')
    classpath tests
    main 'clojure.main'
    args '--main', 'org.graclj.tools.compiler.clojure', tests, tests
}

test {
    dependsOn compileCljTests
    classpath += files(tests)
    testClassesDir = tests
    testLogging {
        events = ['passed', 'failed']
    }
}
