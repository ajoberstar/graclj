import java.nio.file.Files
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'groovy'
}

dependencies {
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile gradleTestKit()
}

ext.samplesDir = file("${buildDir}/samples")

task prepSamples(type: Copy) {
    from 'src/projects'
    into samplesDir
    filter(ReplaceTokens, tokens: [gracljVersion: project.version.toString()])
}

task prepProperties {
    dependsOn prepSamples
    ext.contents = """
clojars_username=${System.env['CLOJARS_USERNAME']}
clojars_password=${System.env['CLOJARS_PASSWORD']}
""".bytes
    doLast {
        Files.list(samplesDir.toPath())
            .filter { Files.isDirectory(it) }
            .map { sampleDir -> sampleDir.resolve('gradle.properties') }
            .forEach { propsFile -> Files.write(propsFile, contents)}
    }
}

test.enabled = false

ext.supportedGradleVersions = ['2.12-20160219000033+0000']

supportedGradleVersions.each { supportedGradleVersion ->
    ext.taskName = "test${supportedGradleVersion}"
    check.dependsOn taskName
    tasks.create(taskName, Test) {
        dependsOn prepProperties
        dependsOn ':modules:graclj-plugin:publishMainPublicationToMavenLocal'
        dependsOn ':modules:graclj-tools:publishMainPublicationToMavenLocal'

        systemProperty 'test.projects.root', samplesDir
        systemProperty 'test.gradle.version', supportedGradleVersion
    }
}
