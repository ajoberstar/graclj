import org.gradle.jvm.test.JUnitTestSuiteSpec

buildscript {
    repositories { mavenLocal() }
    dependencies { classpath 'org.graclj:graclj-plugin:@gracljVersion@' }
}

apply plugin: 'org.graclj.clojure-lang'
apply plugin: 'org.graclj.clojure-test-suite'

repositories {
    mavenLocal()
    jcenter()
}

model {
    components {
        main(JvmLibrarySpec) {
            dependencies {
                module 'org.clojure:clojure:1.8.0'
            }
        }
    }
    testSuites {
        test(JUnitTestSuiteSpec) {
            jUnitVersion '4.12'
            testing $.components.main
        }
    }
}

import org.graclj.internal.GracljInternal
import org.graclj.language.clj.tasks.ClojureCompile

// I'm assuming there's a better way to do this
class MyRules extends RuleSource {
    @Mutate
    void createRunTask(ModelMap<Task> tasks, @Path('binaries.mainAotJar') JarBinarySpec binary, GracljInternal internal) {
        tasks.create('run', JavaExec) {
            dependsOn binary.tasks.jar
            classpath binary.tasks.jar
            classpath internal.resolve(binary.getLibrary().getDependencies())

            main = 'org.graclj.samples.app'
            args 'first', 'second', 'third'
        }
    }
}

apply plugin: MyRules
