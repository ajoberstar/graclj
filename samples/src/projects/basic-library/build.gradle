import org.gradle.jvm.test.JUnitTestSuiteSpec

buildscript {
    repositories { mavenLocal() }
    dependencies { classpath 'org.graclj:graclj-plugin:@gracljVersion@' }
}

apply plugin: 'org.graclj.clojure-lang'
apply plugin: 'org.graclj.clojure-test-suite'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    jcenter()
}

model {
    components {
        main(JvmLibrarySpec) {
            dependencies {
                module 'org.clojure:clojure:1.8.0'
            }
        }
    }
    testSuites {
        test(JUnitTestSuiteSpec) {
            jUnitVersion '4.12'
            testing $.components.main
        }
    }
}

publishing {
    publications {
        main(MavenPublication) {
            groupId = 'org.graclj.samples'
            version = '0.1.0-SNAPSHOT'
            artifact(tasks.createMainJar)
            artifact(tasks.createMainAotJar) {
                classifier = 'aot'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'clojars'
            url = 'https://clojars.org/repo'
            credentials {
                username = clojars_username
                password = clojars_password
            }
        }
    }
}
